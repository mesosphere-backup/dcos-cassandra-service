/*
 * This build file was auto generated by running the Gradle 'init' task
 * by 'mohit' at '2/5/16 1:25 PM' with Gradle 2.10
 *
 * This generated file contains a sample Java project to get you started.
 * For more details take a look at the Java Quickstart chapter in the Gradle
 * user guide available at https://docs.gradle.org/2.10/userguide/tutorial_java_projects.html
 */

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.7.6'
    }
}


plugins {
    id 'nebula.provided-base' version '3.0.3'
}
allprojects {
    apply plugin: 'idea'
    apply plugin: 'eclipse'
    apply plugin: 'nebula.provided-base'
}

idea {
  project {
    jdkName = '1.8'
    languageLevel = '1.8'
    ipr {
      withXml { provider ->
        provider.node.component
          .find { it.@name == 'VcsDirectoryMappings' }
          .mapping.@vcs = 'Git'
      }
    }
  }
}


configure(subprojects - project('seedprovider')) {
    apply plugin: 'java'

    group = 'com.mesosphere.dcos'
    version = '0.1.0'

    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'

    [compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

    repositories {
        mavenLocal()
        mavenCentral()
        maven {
          url "http://downloads.mesosphere.com/maven/"
        }
        maven {
          url "http://downloads.mesosphere.com/maven-snapshot/"
        }
    }

    ext {
        dropwizardVer = '0.9.2'
        guiceVer = '4.0'
        mesosVer = '0.27.1'
        junitVer = '4.12'
        guavaVer = '19.0'
    }

    dependencies {
        compile "org.apache.mesos:mesos:${mesosVer}"
        compile 'org.slf4j:slf4j-api:1.7.13'
        compile "io.dropwizard:dropwizard-core:${dropwizardVer}"
        compile "io.dropwizard.modules:dropwizard-java8:0.9.0-1"
        compile 'io.dropwizard:dropwizard-client:0.9.2'
        compile "com.google.inject:guice:${guiceVer}"
        compile "com.google.guava:guava:${guavaVer}"
        compile "junit:junit:${junitVer}"
        testCompile 'org.apache.curator:curator-test:2.9.1'
        testCompile 'io.dropwizard:dropwizard-testing:0.9.2'
        testCompile 'org.mockito:mockito-all:1.9.5'
    }

    task copyTestResources(type: Copy) {
        from sourceSets.test.resources
        into sourceSets.test.output.classesDir
    }
    processTestResources.dependsOn copyTestResources
}

configure(project(':seedprovider')) {
    apply plugin: 'java'

    group = 'com.mesosphere.dcos'
    version = '0.1.0'

    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'

    [compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

    repositories {
        mavenLocal()
        mavenCentral()
    }

    dependencies {
        compile 'org.apache.cassandra:cassandra-all:2.2.5'

    }
}
